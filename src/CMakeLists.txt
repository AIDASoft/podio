# Helper function for adding two targets. A shared library and a corresponding
# ROOT dictionary that is necessary for e.g. python bindings.
# Arguments:
#   libname         The base name for the library (and target)
#   headers         The header files that should be passed to dictionary generation
#   sources         The source files for the shared libraries
#   selection       The selection xml passed to the dictionary generation
#
# The function creates the following targets
#   <libname>       The shared library. Also available under the podio::<libname> alias
#                   This is not linked against anything and has include directories set
#                   to podio only. So some target_link_libraries are most likely to be
#                   done outside
#   <libname>Dict   The dictionary shared library. This is linked against podio::podio
#                   and the necessary ROOT libraries
#
# Additionally the following files are generated by root
#   - <libname>DictDict.rootmap
#   - <libname>Dict_rdict.pcm
# these files have to be installed to the same directory as the dictionary shared
# library
FUNCTION(PODIO_ADD_LIB_AND_DICT libname headers sources selection )
  # shared library
  add_library(${libname} SHARED ${sources})
  add_library(podio::${libname} ALIAS ${libname})
  target_include_directories(${libname} PUBLIC
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)

  # dictionary
  set(dictname ${libname}Dict)
  add_library(${dictname} SHARED)
  target_include_directories(${dictname} PUBLIC
    $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)
  target_link_libraries(${dictname} PUBLIC podio::${libname} podio::podio ROOT::Core ROOT::Tree)
  PODIO_GENERATE_DICTIONARY(${dictname} ${headers} SELECTION ${selection}
    OPTIONS --library ${CMAKE_SHARED_LIBRARY_PREFIX}${dictname}${CMAKE_SHARED_LIBRARY_SUFFIX}
    )
  # prevent generating dictionary twice
  set_target_properties(${dictname}-dictgen PROPERTIES EXCLUDE_FROM_ALL TRUE)
  target_sources(${dictname} PRIVATE ${dictname}.cxx)
ENDFUNCTION()


# --- Core podio library and dictionary without I/O
SET(core_sources
  CollectionIDTable.cc
  GenericParameters.cc
  ASCIIWriter.cc
  EventStore.cc)

SET(core_headers
  ${CMAKE_SOURCE_DIR}/include/podio/CollectionBase.h
  ${CMAKE_SOURCE_DIR}/include/podio/CollectionIDTable.h
  ${CMAKE_SOURCE_DIR}/include/podio/EventStore.h
  ${CMAKE_SOURCE_DIR}/include/podio/ICollectionProvider.h
  ${CMAKE_SOURCE_DIR}/include/podio/IReader.h
  ${CMAKE_SOURCE_DIR}/include/podio/ObjectID.h
  ${CMAKE_SOURCE_DIR}/include/podio/UserDataCollection.h
  ${CMAKE_SOURCE_DIR}/include/podio/podioVersion.h
  )

PODIO_ADD_LIB_AND_DICT(podio "${core_headers}" "${core_sources}" selection.xml)
target_compile_options(podio PRIVATE -pthread)


# --- Root I/O functionality and corresponding dictionary
SET(root_sources
  rootUtils.h
  ROOTWriter.cc
  ROOTReader.cc
  ROOTFrameWriter.cc
  ROOTFrameReader.cc
  ROOTLegacyReader.cc
)

SET(root_headers
  ${CMAKE_SOURCE_DIR}/include/podio/ROOTFrameReader.h
  ${CMAKE_SOURCE_DIR}/include/podio/ROOTLegacyReader.h
  ${CMAKE_SOURCE_DIR}/include/podio/ROOTFrameWriter.h
  )

PODIO_ADD_LIB_AND_DICT(podioRootIO "${root_headers}" "${root_sources}" root_selection.xml)
target_link_libraries(podioRootIO PUBLIC podio::podio ROOT::Core ROOT::RIO ROOT::Tree)


# --- Python EventStore for enabling (legacy) python bindings
SET(python_sources
  IOHelpers.cc
  PythonEventStore.cc
  )

SET(python_headers
  ${CMAKE_SOURCE_DIR}/include/podio/PythonEventStore.h
)
PODIO_ADD_LIB_AND_DICT(podioPythonStore "${python_headers}" "${python_sources}" python_selection.xml)
target_link_libraries(podioPythonStore PUBLIC podio::podio)
target_link_libraries(podioPythonStore PRIVATE podio::podioRootIO)

# --- SIO I/O functionality and corresponding dictionary
if(ENABLE_SIO)
  SET(sio_sources
    SIOReader.cc
    SIOWriter.cc
    SIOBlockUserData.cc
    SIOBlock.cc
    SIOFrameWriter.cc
    SIOFrameReader.cc
    SIOFrameData.cc
    sioUtils.h
    SIOLegacyReader.cc
    )

  SET(sio_headers
    ${CMAKE_SOURCE_DIR}/include/podio/SIOFrameReader.h
    ${CMAKE_SOURCE_DIR}/include/podio/SIOLegacyReader.h
    ${CMAKE_SOURCE_DIR}/include/podio/SIOFrameWriter.h
    )

  PODIO_ADD_LIB_AND_DICT(podioSioIO "${sio_headers}" "${sio_sources}" sio_selection.xml)
  target_link_libraries(podioSioIO PUBLIC podio::podio SIO::sio ${CMAKE_DL_LIBS} ${PODIO_FS_LIBS})

  # Make sure the legacy python bindings know about the SIO backend
  target_link_libraries(podioPythonStore PRIVATE podioSioIO)
  target_compile_definitions(podioPythonStore PRIVATE PODIO_ENABLE_SIO=1)

  LIST(APPEND INSTALL_LIBRARIES podioSioIO podioSioIODict)
endif()

# --- Install everything
install(TARGETS podio podioDict podioPythonStore podioPythonStoreDict podioRootIO podioRootIODict ${INSTALL_LIBRARIES}
  EXPORT podioTargets
  DESTINATION "${CMAKE_INSTALL_LIBDIR}")

# Only install the necessary headers
if (ENABLE_SIO)
  install(DIRECTORY ${PROJECT_SOURCE_DIR}/include/podio DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")
else()
  install(DIRECTORY ${PROJECT_SOURCE_DIR}/include/podio DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}"
    REGEX SIO.*\\.h$ EXCLUDE )
endif()

install(FILES
  ${CMAKE_CURRENT_BINARY_DIR}/podioDictDict.rootmap
  ${CMAKE_CURRENT_BINARY_DIR}/libpodioDict_rdict.pcm
  ${CMAKE_CURRENT_BINARY_DIR}/podioRootIODictDict.rootmap
  ${CMAKE_CURRENT_BINARY_DIR}/libpodioRootIODict_rdict.pcm
  ${CMAKE_CURRENT_BINARY_DIR}/podioPythonStoreDictDict.rootmap
  ${CMAKE_CURRENT_BINARY_DIR}/libpodioPythonStoreDict_rdict.pcm
  DESTINATION "${CMAKE_INSTALL_LIBDIR}")

if (ENABLE_SIO)
  install(FILES
    ${CMAKE_CURRENT_BINARY_DIR}/podioSioIODictDict.rootmap
    ${CMAKE_CURRENT_BINARY_DIR}/libpodioSioIODict_rdict.pcm
    DESTINATION "${CMAKE_INSTALL_LIBDIR}"
    )
endif()
