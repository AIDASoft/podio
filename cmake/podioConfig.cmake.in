###############################################################################
# PODIO CMAKE CONFIG
#
# Exported Targets
#
#  podio::podio         Base library for podio
#  podio::podioDict     Root Dictionary for base podio classes
#  podio::podioRootIO
#         RootWriter and RootReader classes, needed for python,
#         contains podio::podio and podio::podioDict and the
#         required ROOT:: libraries
#
# Macros: see podioMacros.cmake for complete documentation
#
#  PODIO_GENERATE_DATAMODEL
#  PODIO_GENERATE_ROOTDICT
#
###############################################################################
@PACKAGE_INIT@

set_and_check(podio_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")
set_and_check(podio_LIBRARY_DIR "@PACKAGE_CMAKE_INSTALL_LIBDIR@")
set_and_check(podio_PYTHON_DIR "@PACKAGE_podio_PYTHON_INSTALLDIR@")
set(PODIO_IO_HANDLERS "@PODIO_IO_HANDLERS@")

include(CMakeFindDependencyMacro)
find_dependency(ROOT @ROOT_VERSION@)
if(@REQUIRE_PYTHON_VERSION@)
  find_dependency(Python @REQUIRE_PYTHON_VERSION@ COMPONENTS Interpreter)
else()
  find_dependency(Python COMPONENTS Interpreter)
endif()

SET(ENABLE_SIO @ENABLE_SIO@)
if(ENABLE_SIO)
  find_dependency(SIO)
  # Targets from SIO only become available with v00-01 so we rig them here to be
  # able to use them
  if (SIO_VERSION VERSION_LESS 0.1)
    message(STATUS "Found SIO without Targets, creating them on the fly")
    add_library(SIO::sio SHARED IMPORTED)
    set_target_properties(SIO::sio PROPERTIES
      INTERFACE_INCLUDE_DIRECTORIES ${SIO_INCLUDE_DIRS}
      INTERFACE_LINK_LIBRARIES ${SIO_LIBRARIES}
      IMPORTED_LOCATION ${SIO_LIBRARIES})
  endif()
endif()

if(NOT TARGET podio::podio)
  include("${CMAKE_CURRENT_LIST_DIR}/podioTargets.cmake")
  include("${CMAKE_CURRENT_LIST_DIR}/podioMacros.cmake")
endif()

check_required_components(podio)

# Print the default "Found" message and check library location
include(FindPackageHandleStandardArgs)
get_property(TEST_PODIO_LIBRARY TARGET podio::podio PROPERTY LOCATION)
find_package_handle_standard_args(podio DEFAULT_MSG CMAKE_CURRENT_LIST_FILE TEST_PODIO_LIBRARY)

